plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.10.0"
    id 'org.ajoberstar.reckon' version "0.8.0"
    id "maven-publish"
    id "idea"
}

group 'eu.xenit.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources {
            srcDirs = []
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.6'
}

task integrationTest(type: Test, group: "verification") {
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    systemProperty("eu.xenit.gradle.alfrescosdk.deprecation", "false")
    mustRunAfter(test)
}

check.dependsOn(integrationTest)

gradlePlugin {
    plugins {
        amp {
            id = "eu.xenit.amp"
            implementationClass = "eu.xenit.gradle.alfrescosdk.AmpPlugin"
        }
        
        alfresco {
            id = "eu.xenit.alfresco"
            implementationClass = "eu.xenit.gradle.alfrescosdk.AlfrescoPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/xenit-eu/alfresco-gradle-sdk"
    website = vcsUrl
    description = "Gradle plugins and tasks to support Alfresco development"
    tags = ["alfresco"]
    plugins {
        amp {
            displayName = "Alfresco AMP plugin"
        }
        alfresco {
            displayName = "Alfresco dependency management plugin"
        }
    }

    mavenCoordinates {
        groupId = "eu.xenit.gradle"
        artifactId = "alfresco-sdk"
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn(check)
