plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.12.0"
    id 'org.ajoberstar.reckon' version "0.13.0"
    id "maven-publish"
    id "idea"
}

group 'eu.xenit.gradle'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}



sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
        resources {
            srcDirs = []
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    compile gradleApi()
    testCompile group: 'junit', name: 'junit', version: '4.13.1'
    testCompile group: 'commons-io', name: 'commons-io', version: '2.8.0'
}
import org.gradle.util.GradleVersion
task integrationTest(type: Test, group: "verification") {
    inputs.files fileTree(dir: "$project.projectDir/src/integrationTest", exclude: "**/.gradle")
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    mustRunAfter(test)
    doFirst {
        if (gradle.startParameter.offline) {
            systemProperty "eu.xenit.gradle.alfrescosdk.integration.useGradleVersion", GradleVersion.current().version
        }
    }
}

tasks.withType(Test) {
    systemProperty("eu.xenit.gradle.alfrescosdk.deprecation.stacktrace", "false")
    systemProperty("eu.xenit.gradle.alfrescosdk.deprecation.warnAlways", "true")
}

javadoc {
    exclude("**/internal/**")
}

check.dependsOn(integrationTest)

gradlePlugin {
    plugins {
        ampBase {
            id = "eu.xenit.amp-base"
            implementationClass = "eu.xenit.gradle.alfrescosdk.AmpBasePlugin"
        }
        amp {
            id = "eu.xenit.amp"
            implementationClass = "eu.xenit.gradle.alfrescosdk.AmpPlugin"
        }

        alfresco {
            id = "eu.xenit.alfresco"
            implementationClass = "eu.xenit.gradle.alfrescosdk.AlfrescoPlugin"
        }
    }
}

pluginBundle {
    vcsUrl = "https://github.com/xenit-eu/alfresco-gradle-sdk"
    website = vcsUrl
    description = "Gradle plugins and tasks to support Alfresco development"
    tags = ["alfresco"]
    plugins {
        ampBase {
            displayName = "Alfresco AMP base plugin"
        }
        amp {
            displayName = "Alfresco AMP plugin"
        }
        alfresco {
            displayName = "Alfresco dependency management plugin"
        }
    }

    mavenCoordinates {
        groupId = "eu.xenit.gradle"
        artifactId = "alfresco-sdk"
    }
}

reckon {
    scopeFromProp()
    snapshotFromProp()
}

reckonTagCreate.dependsOn(check)
